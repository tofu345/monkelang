let fibonacci = fn(num) {
    let seen = {0: 0, 1: 1};
    let fib = fn(num) {
        let res = seen[num];
        if (!res) {
            res = fib(num - 1) + fib(num - 2);
            seen[num] = res;
        }
        return res;
    };
    fib(num);
};

let map = fn(f, arr) {
    let iter = fn(arr, accumulated) {
        if (len(arr) == 0) {
            accumulated
        } else {
            iter(rest(arr), push(accumulated, f(first(arr))));
        }
    };
    iter(arr, []);
};

let map2 = fn(f, arr) {
    let result = [null] * len(arr);
    for (let i = 0; i < len(arr); i += 1) {
        result[i] = f(arr[i]);
    }
    return result
};

let reduce = fn(arr, initial, f) {
    let iter = fn(arr, result) {
        if (len(arr) == 0) {
            result
        } else {
            iter(rest(arr), f(result, first(arr)));
        };
    }
    iter(arr, initial);
};
let sum = fn(arr) {
    reduce(arr, 0, fn(initial, el) { initial + el });
};


let numbers = [1, 1 + 1, 4 - 1, 2 * 2, 2 + 3, 12 / 2];
puts("numbers:", numbers);
puts("map(fibonacci, numbers):", map(fibonacci, numbers));

puts("fibonacci of 50:", fibonacci(50));

let a = [1, 2, 3, 4];
let double = fn(x) { x * 2 };
let triple = fn(x) { x * 3 };
puts("a:", a, "double:", map(double, a), "triple:", map2(triple, a));

let arr = [1, 2, 3, 4, 5];
puts("arr:", arr, "sum(arr)", sum(arr));

// vim:ft=javascript:
